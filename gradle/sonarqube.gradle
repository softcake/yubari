/*
 *
 * Copyright 2018 softcake.org.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "org.sonarqube"

sonarqube.properties {
    property "sonar.host.url", project.sonarHostUrl
    property "sonar.login", project.sonarLogin
    property "sonar.projectKey", project.group
    property "sonar.projectName", project.name
    property "sonar.projectVersion", project.version.toString()
    property "sonar.projectDescription", project.description
    property "sonar.language", "java"
    property "sonar.sourceEncoding", "UTF-8"
    property "sonar.links.issue", github.issues
    property "sonar.links.ci", 'https://travis-ci.org/' + github.user + '/' + github.repository
    property "sonar.links.homepage", github.site
    property "sonar.branch", versioning.info.branch
    property "sonar.scm.disabled", Boolean.TRUE

    if (project.isPullRequest && project.hasTravisSecEnv) {
        property "sonar.analysis.mode", "preview"
        property "sonar.issuesReport.html.enable", Boolean.TRUE
        property "sonar.github.pullRequest", System.getenv('TRAVIS_PULL_REQUEST')
        property "sonar.github.repository", System.getenv('TRAVIS_REPO_SLUG')
        property "sonar.github.oauth", System.getenv('SONAR_GITHUB_OAUTH')
    }

}

subprojects {project ->

   sonarqube.skipProject = !(project in rootProject.sonarQubeModules) || !project.isValidBranch

    sonarqube.properties {
        property "sonar.jacoco.reportPaths", ["${project.buildDir}/jacoco/test.exec"]
        property "sonar.sources", "src/main/java"
        property "sonar.java.binaries", "${project.buildDir}/classes"
        //TODO Currently not working! Hardcoded in server
        //property "sonar.junit.reportsPaths",  ["${project.buildDir}/test-results/junit-platform", "${project.buildDir}/test-results/integrationTest"]

        if (project in rootProject.integTestModules) {
            properties["sonar.jacoco.reportPaths"] += "${project.buildDir}/jacoco/integrationTest.exec"
            if (sourceSets.integrationTest.allSource.getSrcDirs() != null) {
                properties["sonar.tests"] += sourceSets.integrationTest.allJava.getSrcDirs()
            }

        }
    }
}


